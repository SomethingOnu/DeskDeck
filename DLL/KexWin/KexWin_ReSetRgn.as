;	ウィンドウ参照・更新 DLL (KexWin.DLL) for HSP
;		ウィンドウ リージョン系
;	    Ver. 2.01 〜 (c) 1999 KLAUS
;|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
;=====================================================================================
;	リージョンは、ウィンドウの可視できる形の事です．
;	通常のウィンドウのリージョンは、長方形になっていますが、
;	　このDLLで、その形を変更できます．
;
;	ウィンドウ上のリージョンの外の部分は、透明になり、
;	　その下のウィンドウや、デスクトップなどを見ることが出来ます．
;=====================================================================================

#include "KexWin.as"

;//--
	alloc msg , 65535 :	msg = ""
	x = (dispx - 360) / 2
	y = (dispy - 120) / 2
	bmpFile = "Rgn2.bmp"
	tX.0 = 0, 35, 95, 145, 230, 260, 310, 340	;透過する色の座標
	tY = 70						;

	sdim moji, 4, 8
	moji.0 = "", "K", "e", "x", "S", "o", "f", "t"

	screen 0 , 360 , 360 , 1, x , y
	    title "HSPウィンドウ 0"
	picload bmpFile

	ReadyRgn 0, 0		;//--リージョン作成・変更の準備をする
	UniteRgn 0 , 0		;//--リージョンを、更新する


;//--画像ファイルを使ってリージョンを作成します
	wait 10

	msg  = "[Rgn2.bmp]を元に'K','e','x','S','o','f','t'を抜いた\n"
	msg += "　リージョンを作成し、再度セットします．"
	dialog msg,,"リージョンメソッド 使用前"

     ;▼それぞれの文字を抜いたリージョンを作成
	repeat 7, 1
		wait 30
		gsel 0, 1
		ReadyRgn 0, cnt		;//--リージョン作成・変更の準備をする

		ReadyHspWinRgn tX.cnt, tY
					;//--HSPウィンドウからリージョン作成の準備

		HspWinRgn cnt, 2	;//--HSPウィンドウからリージョン作成

		UniteRgn 0 , cnt	;//--リージョンを、更新する

		BackUpRgn cnt		;//--バックアップしておく

		SetRgn cnt		;//--リージョンをセット

		title "'"+moji.cnt+"' を透過 - SetRgn"
	loop

	msg  = "ここからは、\n"
	msg += "　今作ったリージョンを\n"
	msg += "　再度セットする．"
	msg += " (無限ループします)"
	dialog msg,,"ReSetRgnする"

     ;▼再度セット
	repeat
	    repeat 7, 1
		wait 30
		ReSetRgn cnt, 0		;//--リージョンを再セット
		SetRgn 0

		title "'"+moji.cnt+"' を透過 - ReSetRgn"
	    loop
	loop

	stop
